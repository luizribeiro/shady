WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

program = { SOI ~ (decl ~ ";")* ~ EOI }

token = @{ ASCII_ALPHA+ }

decl = {
  "pub"?
  ~ (
    (!"infix" ~ token+) |
    ("infix" ~ (token | SYMBOL) ~ token{2})
  )
  ~ "=" ~ expr }

expr = {
  prefix* ~ primary ~ postfix*
  ~ (infix ~ prefix* ~ primary ~ postfix* )*
}

infix =  _{ add | sub | mul | div | pow }
add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
pow = { "^" }

prefix = _{ neg }
neg = { "-" }

postfix = _{ fac }
fac = { "!" }

primary = _{ value | "(" ~ expr ~ ")" }

value = _{ int | str }

int = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }

str = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
