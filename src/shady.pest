WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ single_line_comment | multiline_comment }
single_line_comment = _{ "#" ~ (!(NEWLINE) ~ ANY)* }
multiline_comment = _{ "/*" ~ (!("*/") ~ ANY)* ~ "*/"}

program = { SOI ~ (decl ~ ";")* ~ EOI }

token = @{ (ASCII_ALPHA | "_" | "-")+ }

public = @{ "public" }
infix = @{ "infix" }
fn_name = @{ token }
parameter = @{ "$" ~ token }

decl = {
  public?
  ~ (
    (!infix ~ fn_name ~ parameter*) |
    (infix ~ fn_name ~ parameter{2})
  )
  ~ "=" ~ expr
}

fn_call = { fn_name ~ fn_arg* }
fn_arg = _{ unquoted_str_arg | expr }
unquoted_str_arg = @{ ASCII_ALPHANUMERIC+ }

code_block = { "{" ~ (expr ~ ";")* ~ "}" }

expr = {
  (prefix_op* ~ primary
  ~ (infix_op ~ prefix_op* ~ primary)*)
  | fn_call
  | code_block
}
primary = _{ variable | value | "(" ~ expr ~ ")" }
variable = @{ "$" ~ token }
value = _{ int | str }

infix_op =  @{
  "/" | "*" | "+" | "-" | "%" | "==" | "!=" | "<" | ">" | "<=" | ">="
  | ("`" ~ token ~ "`")
}

prefix_op = @{ "!" | "-" }

int = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }

str = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
