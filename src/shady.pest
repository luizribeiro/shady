WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ single_line_comment | multiline_comment }
single_line_comment = _{ "#" ~ (!(NEWLINE) ~ ANY)* }
multiline_comment = _{ "/*" ~ (!("*/") ~ ANY)* ~ "*/"}

reserved = _{ "if" | "else" | "then" | public | infix | typ }

program = { SOI ~ (fn_definition ~ ";")* ~ EOI }

token = @{ (ASCII_ALPHA | "_" | "-")+ }

public = @{ "public" }
infix = @{ "infix" }
fn_name = @{ !reserved ~ token }
parameter = { variable ~ (":" ~ typ)? }
typ = _{ type_int | type_str | type_bool | type_list }
type_int = @{ "int" }
type_str = @{ "str" }
type_bool = @{ "bool" }
type_list = { "[" ~ typ ~ "]" }

fn_definition = {
  public?
  ~ (
    (!infix ~ fn_name ~ parameter*) |
    (infix ~ fn_name ~ parameter{2})
  )
  ~ "=" ~ expr
}

fn_call = { fn_name ~ fn_arg* }
fn_arg = _{ value | unquoted_str_arg | expr }
unquoted_str_arg = @{ !reserved ~ (ASCII_ALPHANUMERIC | "-" | "/" | "." | "~")+ }

expr = {
  (prefix_op* ~ primary
  ~ (infix_op ~ prefix_op* ~ primary)*)
  | if_expr
}
primary = _{ value | fn_call | variable | list | "(" ~ expr ~ ")" }
variable = @{ "$" ~ token }
value = _{ int | str | bool }

infix_op =  _{ math_infix_op | comparison_infix_op | bool_infix_op | custom_infix_op }
custom_infix_op = @{ ("`" ~ token ~ "`") }
comparison_infix_op = @{ "==" | "!=" | "<" | ">" | "<=" | ">=" }
bool_infix_op = _{ and | or }
and = @{ "&&" }
or = @{ "||" }
math_infix_op = _{ add | sub | mul | div | mod | pow }
add = @{ "+" }
sub = @{ "-" }
mul = @{ "*" }
div = @{ "/" }
mod = @{ "%" }
pow = @{ "^" }

prefix_op = @{ "!" | "-" }

if_expr = { "if" ~ "(" ~ expr ~ ")" ~ expr ~ ";"? ~ "else" ~ expr }

int = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }

str = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

bool = @{ "true" | "false" }

list = { "[" ~ (expr ~ ";")* ~ expr? ~ "]" }
